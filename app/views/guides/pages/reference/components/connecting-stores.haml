- set_meta_tags title: 'Guide / Reference / Components / Connecting Stores'

%h1 Connecting Stores

%p
  Components refer to
  %a(href="/guide/reference/stores")
    stores
  in order to
  %b use functions and properties from the store.

%p
  The component references a store with the
  %code connect
  keyword:

%pre
  %code
    :escaped
      store Counter {
        state count : Number = 0

        fun setCount (count : Number) : Promise(Never, Void) {
          next { count = count }
        }
      }

      component Main {
        connect Counter exposing { count, setCount }

        fun handleClick (event : Html.Event) : Promise(Never, Void) {
          setCount(count + 1)
        }

        fun handleContextMenu (event : Html.Event) : Promise(Never, Void) {
          setCount(0)
        }

        fun render : Html {
          <div
            onContextMenu={handleContextMenu}
            onClick={handleClick}>

            <{ "Count: " + Number.toString(count) }>

          </div>
        }
      }

%p
  When connecting a store, the component must use the
  %code exposing
  keyword to list the particular functions or properties it will use.

%h2 Exposing with a different name

%p
  You can expose a connected function or property by a different name using the
  %code as
  notation:

%pre
  %code
    :escaped
      connect Counter exposing { count as myCount }

